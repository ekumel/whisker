import Quickshell.Services.SystemTray
import QtQuick
import Quickshell
import Quickshell.Widgets
import QtQuick.Layouts
import qs.modules
import qs.components
import qs.preferences
import qs.services as Serv

Item {
    id: root
    readonly property Repeater items: itemsRep
    property bool verticalMode: false
    Layout.alignment: verticalMode ? Qt.AlignHCenter : Qt.AlignVCenter
    visible: layout.children.length > 0
    clip: true
    implicitWidth: bg.width
    implicitHeight: bg.height

    StyledRectangle {
        id: bg
        implicitWidth: root.verticalMode
            ? 25
            : (layout.implicitWidth > 0 ? layout.implicitWidth + 10 : 0)
        implicitHeight: root.verticalMode
            ? (layout.implicitHeight > 0 ? layout.implicitHeight + 10 : 0)
            : 25
        radius: 20
        color: Appearance.colors.m3surface_container
        opacity: !Preferences.bar.keepOpaque && !Serv.Hyprland.currentWorkspace.hasTilingWindow() ? 0 : 1

        Behavior on implicitWidth {
            NumberAnimation {
                duration: Appearance.animation.fast
                easing.type: Appearance.animation.easing
            }
        }
        Behavior on implicitHeight {
            NumberAnimation {
                duration: Appearance.animation.fast
                easing.type: Appearance.animation.easing
            }
        }
    }

    GridLayout {
        id: layout
        anchors.centerIn: parent
        rows: root.verticalMode ? 1 : 4
        columns: root.verticalMode ? 1 : 4
        rowSpacing: 10
        columnSpacing: 10

        Repeater {
            id: itemsRep
            model: SystemTray.items

            delegate: Item {
                id: trayItemRoot
                required property SystemTrayItem modelData
                implicitWidth: 16
                implicitHeight: 16

                IconImage {
                    source: {
                        let icon = trayItemRoot.modelData.icon
                        if (icon.includes("?path=")) {
                            const [name, path] = icon.split("?path=")
                            icon = `file://${path}/${name.slice(name.lastIndexOf("/") + 1)}`
                        }
                        return icon
                    }
                    asynchronous: true
                    anchors.fill: parent
                }

                HoverHandler { id: hover }

                QsMenuOpener {
                    id: menuOpener
                    menu: trayItemRoot.modelData.menu
                }

                StyledPopout {
                    id: popout
                    hoverTarget: hover
                    interactable: true
                    hCenterOnItem: true
                    requiresHover: false

                    Component {
                        Item {
                            width: childColumn.implicitWidth
                            height: childColumn.height

                            ColumnLayout {
                                id: childColumn
                                spacing: 5

                                Repeater {
                                    model: menuOpener.children
                                    delegate: TrayMenuItem {
                                        parentColumn: childColumn
                                        Layout.preferredWidth: childColumn.width > 0 ? childColumn.width : implicitWidth
                                    }
                                }
                            }
                        }
                    }
                }

                MouseArea {
                    anchors.fill: parent
                    acceptedButtons: Qt.RightButton
                    hoverEnabled: true

                    onClicked: {
                        if (popout.isVisible)
                            popout.hide()
                        else
                            popout.show()
                    }
                }
            }
        }
    }

    component TrayMenuItem: Item {
        id: itemRoot
        required property QsMenuEntry modelData
        required property ColumnLayout parentColumn

        Layout.fillWidth: true
        implicitWidth: rowLayout.implicitWidth + 10
        implicitHeight: !itemRoot.modelData.isSeparator ? rowLayout.implicitHeight + 10 : 1

        MouseArea {
            id: hover
            hoverEnabled: itemRoot.modelData.enabled
            anchors.fill: parent
            onClicked: {
                if (!itemRoot.modelData.hasChildren)
                    itemRoot.modelData.triggered()
            }
        }

        StyledRectangle {
            id: itemBg
            anchors.fill: parent
            opacity: itemRoot.modelData.isSeparator ? 0.5 : 1
            color: itemRoot.modelData.isSeparator
                   ? Appearance.colors.m3outline
                   : hover.containsMouse ? Appearance.colors.m3surface_container : Appearance.colors.m3surface
        }

        RowLayout {
            id: rowLayout
            visible: !itemRoot.modelData.isSeparator
            opacity: itemRoot.modelData.isSeparator ? 0.5 : 1
            spacing: 5
            anchors {
                left: itemBg.left
                leftMargin: 5
                top: itemBg.top
                topMargin: 5
            }

            IconImage {
                visible: itemRoot.modelData.icon !== ""
                source: itemRoot.modelData.icon
                width: 15
                height: 15
            }

            StyledText {
                text: itemRoot.modelData.text
                font.pixelSize: 14
                color: Appearance.colors.m3on_surface
            }

            MaterialIcon {
                visible: itemRoot.modelData.hasChildren
                icon: "chevron_right"
                font.pixelSize: 16
                color: Appearance.colors.m3on_surface
            }
        }
    }
}
